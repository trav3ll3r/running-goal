def androidx = rootProject.ext.androidx
def libs = rootProject.ext.libs
def kotlin = rootProject.ext.kotlin
def test = rootProject.ext.testing

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "au.com.beba.runninggoal"
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            // Sign with debug key
            storeFile file("src/debug/debug_certificate.jks")
            storePassword "password"
            keyAlias "debug_key"
            keyPassword "password"
        }
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "dataSource"
    productFlavors {
        real {
            dimension "dataSource"
        }

        mock {
            dimension "dataSource"
        }
    }

    compileOptions {
        sourceCompatibility '1.8'
        targetCompatibility '1.8'
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:${androidx.appcompat}"
    implementation "androidx.constraintlayout:constraintlayout:${androidx.constraintLayoutVer}"
    implementation "androidx.recyclerview:recyclerview:${androidx.recyclerViewVer}"
    implementation "androidx.lifecycle:lifecycle-extensions:${androidx.archLifecycleVer}"
    kapt "androidx.lifecycle:lifecycle-compiler:${androidx.archLifecycleVer}"

    implementation "com.google.android.material:material:${androidx.materialVer}"
}

// DAGGER2 dependencies
dependencies {
    implementation "com.google.dagger:dagger:${libs.daggerVersion}"
    implementation "com.google.dagger:dagger-android:${libs.daggerVersion}"
    implementation "com.google.dagger:dagger-android-support:${libs.daggerVersion}"
    kapt "com.google.dagger:dagger-compiler:${libs.daggerVersion}"
    kapt "com.google.dagger:dagger-android-processor:${libs.daggerVersion}"
    implementation "javax.inject:javax.inject:${libs.javaxInjectVersion}"
}

// KOTLIN, ANKO, KOTLINX
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin.kotlinVer}"
    implementation "org.jetbrains.anko:anko-sdk25:${kotlin.ankoVer}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${kotlin.coroutines}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${kotlin.coroutines}"
    implementation "org.jetbrains.anko:anko-coroutines:${kotlin.ankoVer}"
    implementation "org.jetbrains.anko:anko-support-v4:${kotlin.ankoVer}"
    implementation "org.jetbrains.anko:anko-appcompat-v7:${kotlin.ankoVer}"
    implementation "androidx.core:core-ktx:${kotlin.coreKtx}"
}

// MODULES
dependencies {
    implementation project(':domain_goal')
    implementation project(':domain_workout')
    implementation project(':domain_widget')
    implementation project(':domain_event')
    implementation project(':repo_workout') // ABSTRACT WITH feature_workout
    implementation project(':feature_base')
    implementation project(':feature_goal')
    implementation project(':feature_sync')
    implementation project(':feature_widget')
    implementation project(':stylist')
    implementation project(':ui_component')
}

// TESTING
dependencies {
    testImplementation "junit:junit:${test.junit}"
    androidTestImplementation 'androidx.test:runner:1.1.0-beta02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-beta02'
}

// UTILS (CRASHLYTICS, STETHO)
dependencies {
    implementation("com.crashlytics.sdk.android:crashlytics:${libs.crashlytics}") {
        transitive = true
    }
    implementation "com.facebook.stetho:stetho:${libs.stetho}"
}

// LOGGING
dependencies {
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'at.favre.lib:slf4j-timber:1.0.0'    //slf4j BINDINGS FOR Timber
}